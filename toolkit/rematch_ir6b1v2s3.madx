option, warn,info;
system,"rm -rf temp"; system,"mkdir temp";
system,"ln -fns ../../hllhc13 slhc";
system,"ln -fns ../../lhcrunIII lhc";

setplot, post=1;

Option, -echo,-info,-warn;
call,file="lhc/lhc.seq";


NRJ=7000;
gamma_rel      := NRJ/pmass;
value, gamma_rel, pmass;
if (emittance_norm==0){ emittance_norm = 2.5e-6; };
bunch_len=0.0755;
epsx:=emittance_norm /gamma_rel;
epsy:=emittance_norm /gamma_rel;

value, epsx, epsy;

if (mylhcbeam>2){ bv_aux=-1; } else { bv_aux=1; };

Beam,particle=proton,energy=NRJ,             NPART=2.2E11,sige=4.5e-4*sqrt(450./NRJ),ex:=epsx,ey:=epsy,sigt:=bunch_len;


call,file="ir6b1.seqx";

Option, echo,info,warn;

use,sequence=lhcb1ir6;


!constants needed for calculation of the constraint 6 (2017) from Chiara
chi6_nsig = 10.1; 
chi6_dPoverP = 2e-4; 
chi6_emitx=2.5e-6/(7000/0.9382720814); 
chi6_maxorbitdrift = 1.2e-3;


scale = 23348.89927;
scmin := 0.03*7000./nrj;
sch=0.99;
qtlimit2 := sch*160.0/scale;
qtlimit3 := sch*200.0/scale;
qtlimit4 := sch*125.0/scale;
qtlimit5 := sch*120.0/scale;
qtlimit6 := sch*90.0/scale;


if(on_fixedip==1){
muxip6b1 = 2.0000 ;betxip6b1= 187.297499 ;alfxip6b1 = -0.541994 ;
muyip6b1 = 2.0300 ;betyip6b1= 168.122917 ;alfyip6b1 =  0.605891 ;
dxip6b1 =  0.1400 ;dpxip6b1 =   0 ;
};

value,muxip6b1,betxip6b1,alfxip6b1;
value,muyip6b1,betyip6b1,alfyip6b1;
value,dxip6b1,dpxip6b1;

!constants needed for calculation of the constraint 6 (2017) from Chiara
chi6_nsig = 10.1; 
chi6_dPoverP = 2e-4; 
chi6_emitx=2.5e-6/(7000/0.9382720814); 
chi6_maxorbitdrift = 1.2e-3;

maxmuxshift = 0.2;
maxmuyshift = 0.2;



call,file="opt_100_400_400_100_v8.madx";
call, file=betablock.ir6b1.madx;


value, kqt13.l6b1,
       kqt12.l6b1 ,
       kqtl11.l6b1,
       kq10.l6b1  ,
       kq9.l6b1   ,
       kq8.l6b1   ,
       kq5.l6b1   ,
       kq4.l6b1   ,
       kq4.r6b1   ;

use,sequence=lhcb1ir6;

title,"Start";
twiss,beta0=bir6b1;

value, table(twiss, ip6, betx); 

plot, noversion, haxis=s, vaxis=betx,bety, colour=100,, vmax=1100;
plot, noversion, range=s.ds.l6.b1/e.ds.r6.b1, haxis=s, vaxis=betx,bety, colour=100;
plot, noversion, range=s.ds.l6.b1/e.ds.r6.b1, haxis=s, vaxis=mux,muy, colour=100;
plot, noversion, range=s.ds.l6.b1/e.ds.r6.b1, haxis=s, vaxis=dx, colour=100;

select,flag=twiss,clear;
select,flag=twiss,column=name,s,betx,bety,mux,dx,dpx;



write, table=twiss, file="ir6b1.twiss";


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


!call, file="opt_ir6b1v2s2.madx";
call, file="opt_ir6b1v2s3.madx";

use,sequence=lhcb1ir6, range=TCDQA.A4R6.b1/#e;

title,"TCDQA.A4R6.b1  to eir6b1";

 twiss,betx = bx_tcdq,
       bety = by_tcdq,
       alfx = ax_tcdq,
       alfy = ay_tcdq,
       dx =   dx_tcdq,
       dpx =  dpx_tcdq;

      
write, table=twiss, file="bir6b1_mkdh.twiss";
plot, noversion, haxis=s, vaxis=betx,bety, colour=100;
plot, noversion, haxis=s, vaxis=dx,dpx, colour=100;



match,use_macro;


    vary,name=kq4.r6b1,    step=1.0E-6, lower= qtlimit2*scmin, upper= qtlimit2;
    vary,name=kq5.r6b1,    step=1.0E-6, lower=-qtlimit3*0.99, upper=-qtlimit3*scmin;
    vary,name=kq8.r6b1,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
    vary,name=kq9.r6b1,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
    vary,name=kq10.r6b1,   step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
    vary,name=kqtl11.r6b1, step=1.0E-6, lower=-qtlimit4*300./550., upper=qtlimit4*300./550.;
    vary,name=kqt12.r6b1,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
    vary,name=kqt13.r6b1,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;

    m1:macro=
     {
       twiss,betx = bx_tcdq,
             bety = by_tcdq,
             alfx = ax_tcdq,
             alfy = ay_tcdq,
             dx =   dx_tcdq,
             dpx =  dpx_tcdq;
     };

    !!!!!!!!!!!!!!!!  QUAD LIMITS !!!!!!!!!!!!!!!

    constraint, weight=1e3, expr= kq4.r6b1     >  1.01 *  qtlimit2*scmin;     
    constraint, weight=1e3, expr= kq5.r6b1     > -0.99 *  qtlimit3;     
    constraint, weight=1e3, expr= kq8.r6b1     >  1.01 *  qtlimit3*scmin;           
    constraint, weight=1e3, expr= kq9.r6b1     > -0.99 *  qtlimit3;    
    constraint, weight=1e3, expr= kq10.r6b1    >  1.01 *  qtlimit3*scmin;     
    constraint, weight=1e3, expr= kqtl11.r6b1  > -0.99 *  qtlimit4*300./550.;
    constraint, weight=1e3, expr= kqt12.r6b1   > -0.995 *  qtlimit5;
    constraint, weight=1e3, expr= kqt13.r6b1   > -0.995 *  qtlimit5;

    constraint, weight=1e3, expr= kq4.r6b1     <  0.99 *  qtlimit2;
    constraint, weight=1e3, expr= kq5.r6b1     < -1.01 *  qtlimit3*scmin;     
    constraint, weight=1e3, expr= kq8.r6b1     <  0.99 *  qtlimit3;
    constraint, weight=1e3, expr= kq9.r6b1     < -1.01 *  qtlimit3*scmin;    
    constraint, weight=1e3, expr= kq10.r6b1    <  0.99 *  qtlimit3;
    constraint, weight=1e3, expr= kqtl11.r6b1  <  0.99 *  qtlimit4*300./550.;
    constraint, weight=1e3, expr= kqt12.r6b1   <  0.995 *  qtlimit5;
    constraint, weight=1e3, expr= kqt13.r6b1   <  0.995 *  qtlimit5;


    !!!!!!!!!!!!!!!!  END OF IR6 / start of arc67 conditions  !!!!!!!!!!!!!!!

    constraint, weight=1e1, expr = table(twiss, E.DS.R6.B1, betx) = eir6b1->betx;
    constraint, weight=1e1, expr = table(twiss, E.DS.R6.B1, bety) = eir6b1->bety;

    constraint, weight=1e1, expr = table(twiss, E.DS.R6.B1, alfx) = eir6b1->alfx;
    constraint, weight=1e1, expr = table(twiss, E.DS.R6.B1, alfy) = eir6b1->alfy;

    constraint, weight=1e1, expr = table(twiss, E.DS.R6.B1, dx)   = eir6b1->dx;
    constraint, weight=1e1, expr = table(twiss, E.DS.R6.B1, dpx)  = eir6b1->dpx;

    

!    constraint, weight=1e-2, expr = table(twiss, MQML.10L6.B1, bety) < 870;





    jacobian,calls=jac_calls, tolerance=jac_tol, bisec=jac_bisec;

    lmdif,calls=3500,tolerance=jac_tol;
    simplex,calls=1500,tolerance=jac_tol;
    lmdif,calls=3500,tolerance=jac_tol;
endmatch;

tarStep1 = tar;



title,"matched bir6b1 to MKD.H5L6.B1 ";

 twiss,betx = bx_tcdq,
       bety = by_tcdq,
       alfx = ax_tcdq,
       alfy = ay_tcdq,
       dx =   dx_tcdq,
       dpx =  dpx_tcdq;

plot, noversion, haxis=s, vaxis=betx,bety, colour=100, interpolate;
plot, noversion, haxis=s, vaxis=dx,dpx, colour=100, interpolate;



title,"matched IR6";
use,sequence=lhcb1ir6;

twiss,beta0 = bir6b1;

plot, noversion, haxis=s, vaxis=betx,bety, colour=100, interpolate;
plot, noversion, haxis=s, vaxis=dx,dpx, colour=100, interpolate;




assign, echo=opt_ir6b1v2s3.madx, truncate=true;
print, text="! opt_ir6b1v2s3.madx";

printf,text=" bx_mkd      =   %20.15F ;",value= bx_mkd  ;
printf,text=" by_mkd      =   %20.15F ;",value= by_mkd  ;
printf,text=" ax_mkd      =   %20.15F ;",value= ax_mkd  ;
printf,text=" ay_mkd      =   %20.15F ;",value= ay_mkd  ;
printf,text=" dx_mkd      =   %20.15F ;",value= dx_mkd  ;
printf,text=" dpx_mkd     =   %20.15F ;",value= dpx_mkd  ;

printf,text=" bx_tcdq     =   %20.15F ;",value= bx_tcdq  ;
printf,text=" by_tcdq     =   %20.15F ;",value= by_tcdq  ;
printf,text=" ax_tcdq     =   %20.15F ;",value= ax_tcdq  ;
printf,text=" ay_tcdq     =   %20.15F ;",value= ay_tcdq  ;
printf,text=" dx_tcdq     =   %20.15F ;",value= dx_tcdq  ;
printf,text=" dpx_tcdq    =   %20.15F ;",value= dpx_tcdq ;


printf,text=" kqt13.l6b1  =   %20.15F ;",value=kqt13.l6b1  ;
printf,text=" kqt12.l6b1  =   %20.15F ;",value=kqt12.l6b1  ;
printf,text=" kqtl11.l6b1 =   %20.15F ;",value=kqtl11.l6b1 ;
printf,text=" kq10.l6b1   =   %20.15F ;",value=kq10.l6b1   ;
printf,text=" kq9.l6b1    =   %20.15F ;",value=kq9.l6b1    ;
printf,text=" kq8.l6b1    =   %20.15F ;",value=kq8.l6b1	;
printf,text=" kq5.l6b1    =   %20.15F ;",value=kq5.l6b1	;

printf,text=" kq4.r6b1    =   %20.15F ;",value=kq4.r6b1   ;
printf,text=" kq5.r6b1    =   %20.15F ;",value=kq5.r6b1   ;
printf,text=" kq8.r6b1    =   %20.15F ;",value=kq8.r6b1   ;
printf,text=" kq9.r6b1    =   %20.15F ;",value=kq9.r6b1   ;
printf,text=" kq10.r6b1   =   %20.15F ;",value=kq10.r6b1  ;
printf,text=" kqtl11.r6b1 =   %20.15F ;",value=kqtl11.r6b1 ;
printf,text=" kqt12.r6b1  =   %20.15F ;",value=kqt12.r6b1  ;
printf,text=" kqt13.r6b1  =   %20.15F ;",value=kqt13.r6b1  ;











assign,echo=terminal;



value, bx_mkd,
       by_mkd,
       ax_mkd,
       ay_mkd,
       table(twiss, TCDQA.A4R6.b1, mux);

value, table(twiss, TCDQA.A4R6.b1, mux), table(twiss, TCDQA.A4R6.b1, alfx), tarStep1, tarStep2; 



stop;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!  SSSSS  TTTTT   OOO   PPP
!  S        T    O   O  P  P
!  SSSSS    T    O   O  PPP
!      S    T    O   O  P
!  SSSSS    T     OOO   P
!

