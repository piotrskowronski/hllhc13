option, warn,info;
system,"rm -rf temp"; system,"mkdir temp";
system,"ln -fns ../../hllhc13 slhc";
system,"ln -fns ../../lhcrunIII lhc";

setplot, post=1;

Option, -echo,-info,-warn;
call,file="lhc/lhc.seq";


NRJ=7000;
gamma_rel      := NRJ/pmass;
value, gamma_rel, pmass;
if (emittance_norm==0){ emittance_norm = 2.5e-6; };
bunch_len=0.0755;
epsx:=emittance_norm /gamma_rel;
epsy:=emittance_norm /gamma_rel;

value, epsx, epsy;

if (mylhcbeam>2){ bv_aux=-1; } else { bv_aux=1; };
Beam,particle=proton,energy=NRJ,bv = -bv_aux,NPART=2.2E11,sige=4.5e-4*sqrt(450./NRJ),ex:=epsx,ey:=epsy,sigt:=bunch_len;


call,file="ir6b2.seqx";

!Option, echo,info,warn;

use,sequence=lhcb2ir6;


!constants needed for calculation of the constraint 6 (2017) from Chiara
chi6_nsig = 10.1; 
chi6_dPoverP = 2e-4; 
chi6_emitx=2.5e-6/(7000/0.9382720814); 
chi6_maxorbitdrift = 1.2e-3;


scale = 23348.89927;
scmin := 0.03*7000./nrj;
sch=0.99;
qtlimit2 := sch*160.0/scale;
qtlimit3 := sch*200.0/scale;
qtlimit4 := sch*125.0/scale;
qtlimit5 := sch*120.0/scale;
qtlimit6 := sch*90.0/scale;


if(on_fixedip==1){
muxip6b2 = 2.0000 ;betxip6b2= 187.749224;alfxip6b2 = 0.551968;
muyip6b2 = 2.0300 ;betyip6b2= 178.368556;alfyip6b2 =-0.607183;
dxip6b2 =  0.0;dpxip6b2 = 0 ;!value HLLHCV1.0/HLLHCV1.1: dxip6b2 =  0.186;
};

value,muxip6b2,betxip6b2,alfxip6b2;
value,muyip6b2,betyip6b2,alfyip6b2;
value,dxip6b2,dpxip6b2;

!constants needed for calculation of the constraint 6 (2017) from Chiara
chi6_nsig = 10.1; 
chi6_dPoverP = 2e-4; 
chi6_emitx=2.5e-6/(7000/0.9382720814); 
chi6_maxorbitdrift = 1.2e-3;

maxmuxshift = 0.2;
maxmuyshift = 0.2;



call,file="opt_100_400_400_100_v4.madx";
call, file=betablock.ir6b2.madx;

call,file=opt_ir6b2s5.madx;

value, kqt13.l6b2,
       kqt12.l6b2 ,
       kqtl11.l6b2,
       kq10.l6b2  ,
       kq9.l6b2   ,
       kq8.l6b2   ,
       kq5.l6b2   ,
       kq4.l6b2   ,
       kq4.r6b2   ;

use,sequence=lhcb2ir6;

title,"Start";
twiss,beta0=bir6b2;


     twiss,beta0=bir6b2;
     value, table(twiss, lhcb2ir6, betx); 

plot, haxis=s, vaxis=betx,bety, colour=100,, vmax=1100;
plot, range=s.ds.l6.b2/e.ds.r6.b2, haxis=s, vaxis=betx,bety, colour=100;
plot, range=s.ds.l6.b2/e.ds.r6.b2, haxis=s, vaxis=mux,muy, colour=100;
plot, range=s.ds.l6.b2/e.ds.r6.b2, haxis=s, vaxis=dx, colour=100;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


twiss,beta0=bir6b2;
value, table(twiss, lhcb2ir6, betx); 

match,use_macro;
 vary,name=kqt13.l6b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
 vary,name=kqt12.l6b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
 vary,name=kqtl11.l6b2, step=1.0E-6, lower=-qtlimit4*400./550., upper=qtlimit4*400./550.;
 vary,name=kq10.l6b2,   step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq9.l6b2,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq8.l6b2,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq5.l6b2,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq4.l6b2,    step=1.0E-6, lower= qtlimit2*scmin, upper= qtlimit2;

!fixed
 !vary,name=kq4.r6b2,    step=1.0E-6, lower=-qtlimit2, upper=-qtlimit2*scmin;!fixed for septum


 vary,name=kq5.r6b2,    step=1.0E-6, lower= qtlimit2*scmin, upper= qtlimit2;
 vary,name=kq8.r6b2,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq9.r6b2,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq10.r6b2,   step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kqtl11.r6b2, step=1.0E-6, lower=-qtlimit4*300./550., upper=qtlimit4*300./550.;
 vary,name=kqt12.r6b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
 vary,name=kqt13.r6b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;



 m:macro=
  {
     twiss,beta0=bir6b2;
    ! value, table(twiss, ip6, dpx); 

   refdmuxkickb2_tcdq=table(twiss,MKD.H5R6.B2,mux)-table(twiss,TCDQA.C4L6.B2,mux);

   betxtcdqb2=table(twiss,TCDQA.A4L6.B2,betx);
   refdxtcdqb2=table(twiss,TCDQA.A4L6.B2,dx);

   tcdqmingapb2 =   chi6_nsig*sqrt(chi6_emitx*betxtcdqb2) 
                  - 3e-4  
                  - abs(refdxtcdqb2*chi6_dPoverP) 
                  - chi6_maxorbitdrift;
   
   
  };

  !!!!!!!!!!!!!!!!  QUAD LIMITS !!!!!!!!!!!!!!!

  constraint, weight=1e2, expr= kqt13.l6b2  > -0.995 * qtlimit5; 
  constraint, weight=1e2, expr= kqt12.l6b2  > -0.995 * qtlimit5; 
  constraint, weight=1e2, expr= kqtl11.l6b2 > -0.99 * qtlimit4*400./550.;
  constraint, weight=1e2, expr= kq10.l6b2   >  1.01 * qtlimit3*scmin; 
  constraint, weight=1e2, expr= kq9.l6b2    > -0.99 * qtlimit3; 
  constraint, weight=1e2, expr= kq8.l6b2    >  1.01 * qtlimit3*scmin;
  constraint, weight=1e2, expr= kq5.l6b2    > -0.99 * qtlimit3; 
  constraint, weight=1e2, expr= kq4.l6b2    >  1.005 * qtlimit2*scmin;

  constraint, weight=1e2, expr= kqt13.l6b2  <  0.995 * qtlimit5; 
  constraint, weight=1e2, expr= kqt12.l6b2  <  0.995 * qtlimit5; 
  constraint, weight=1e2, expr= kqtl11.l6b2 <  0.99 * qtlimit4*400./550.;
  constraint, weight=1e2, expr= kq10.l6b2   <  0.99 * qtlimit3;
  constraint, weight=1e2, expr= kq9.l6b2    < -1.01 * qtlimit3*scmin; 
  constraint, weight=1e2, expr= kq8.l6b2    <  0.99 * qtlimit3;
  constraint, weight=1e2, expr= kq5.l6b2    < -1.01 * qtlimit3*scmin; 
  constraint, weight=1e2, expr= kq4.l6b2    <  0.99 * qtlimit2;
  

  constraint, weight=1e2, expr= kq5.r6b2     >  1.01 *  qtlimit3*scmin;     
  constraint, weight=1e2, expr= kq8.r6b2     > -0.99 *  qtlimit3;           
  constraint, weight=1e2, expr= kq9.r6b2     >  1.01 *  qtlimit3*scmin;    
  constraint, weight=1e2, expr= kq10.r6b2    > -0.99 *  qtlimit3;     
  constraint, weight=1e2, expr= kqtl11.r6b2  > -0.99 *  qtlimit4*300./550.;
  constraint, weight=1e2, expr= kqt12.r6b2   > -0.995 *  qtlimit5;

  constraint, weight=1e2, expr= kq5.r6b2     <  0.99 *  qtlimit3;     
  constraint, weight=1e2, expr= kq8.r6b2     < -1.01 *  qtlimit3*scmin;           
  constraint, weight=1e2, expr= kq9.r6b2     <  0.99 *  qtlimit3;    
  constraint, weight=1e2, expr= kq10.r6b2    < -1.01 *  qtlimit3*scmin;     
  constraint, weight=1e2, expr= kqtl11.r6b2  <  0.99 *  qtlimit4*300./550.;
  constraint, weight=1e2, expr= kqt12.r6b2   <  0.995 *  qtlimit5;
  
  

!  constraint,weight=1e1, sequence=lhcb2,range=ip6,dx=0,dpx=0;
    
    constraint, weight=1e1, expr= abs(table(twiss, ip6, dx ))  < 5e-1;
    constraint, weight=1e5, expr= table(twiss, ip6, dpx ) <  1e-4;
    constraint, weight=1e5, expr= table(twiss, ip6, dpx ) > -1e-4;

    constraint, expr= abs(table(twiss, bpmsa.4l6.b2, dx ))  < 2e-1;
    constraint, expr= abs(table(twiss, bpmsa.4l6.b2, dpx )) < 2e-1;
    
    constraint,range=mqml.8l6.b2,  betx < 800;
    constraint,range=mqml.8l6.b2,  bety < 800;
    constraint,range=mqm.9l6.b2,   betx < 800;
    constraint,range=mqm.9l6.b2,   bety < 800;

    constraint,range=mqy.5l6.b2,   bety < 1500;

    constraint,range=mqy.4l6.b2,   betx < 1200;


    constraint,range=mqy.4r6.b2,   betx < 1000;
    constraint,range=mqy.4r6.b2,   bety < 1000;


    constraint,range=ip6,   betx < 500;
    constraint,range=ip6,   bety < 500;



     !!!!!!!!!!!!!!!  C2  !!!!!!!!!!!!!!!!!!! 
     constraint,weight=1e5,expr= abs(refdmuxkickb2_tcdq-0.250)<0.0111;

  
       !!!!!!!!!!!! C3 !!!!!!!!!!!!!!!
     constraint,sequence=lhcb2,range=TCDSA.4R6.B2,bety>200;
     
     !!!!!!!!!!!!! C4 !!!!!!!!!!!!!!!
     constraint,sequence=lhcb2,range=TCDQA.A4L6.B2,bety>145;


     !!!!!!!!!!!!!!! C6 !!!!!!!!!!!!!!!!!

     constraint,weight=1e1,sequence=lhcb2,range=TCDQA.A4L6.B2,betx>800;

     constraint, weight=1e4, expr= tcdqmingapb2 > 3e-3;
     
    !!!!!!!!!!!!!!! C8 !!!!!!!!!!!!!!!!!
   
     constraint, weight=1e4, expr= table(twiss,MKD.H5R6.B2,mux) < 1.00;
     

  !!!!!!!!!!!! MATCH TO ARC
  constraint,weight=1e1, range=e.ds.r6.b2,alfx=eir6b2->alfx,alfy=eir6b2->alfy;
!  constraint,weight=1e-1, range=e.ds.r6.b2,betx=eir6b2->betx,bety=eir6b2->bety;
  constraint,weight=1e1, range=e.ds.r6.b2,dx=eir6b2->dx,dpx=eir6b2->dpx;

  
!  jacobian,calls=jac_calls, tolerance=jac_tol, bisec=jac_bisec;
  lmdif,calls=1500,tolerance=jac_tol;
  simplex,  calls=15, tolerance=1e-8;
  lmdif,calls=1500,tolerance=jac_tol;
  
endmatch;















assign, echo=opt_ir6b2s5.madx, truncate=true;
print, text="! betablock.ir6";
printf,text=" kqt13.l6b2   =   %F ;",value= kqt13.l6b2  ;
printf,text=" kqt12.l6b2   =   %F ;",value= kqt12.l6b2  ;
printf,text=" kqtl11.l6b2  =   %F ;",value= kqtl11.l6b2 ;
printf,text=" kq10.l6b2    =   %F ;",value= kq10.l6b2   ;
printf,text=" kq9.l6b2     =   %F ;",value= kq9.l6b2    ;
printf,text=" kq8.l6b2     =   %F ;",value= kq8.l6b2    ;
printf,text=" kq5.l6b2     =   %F ;",value= kq5.l6b2    ;
printf,text=" kq4.l6b2     =   %F ;",value= kq4.l6b2    ;
printf,text=" kq4.r6b2     =   %F ;",value= kq4.r6b2    ;
printf,text=" kq5.r6b2     =   %F ;",value= kq5.r6b2    ;
printf,text=" kq8.r6b2     =   %F ;",value= kq8.r6b2    ;
printf,text=" kq9.r6b2     =   %F ;",value= kq9.r6b2    ;
printf,text=" kq10.r6b2    =   %F ;",value= kq10.r6b2   ;
printf,text=" kqtl11.r6b2  =   %F ;",value= kqtl11.r6b2 ;
printf,text=" kqt12.r6b2   =   %F ;",value= kqt12.r6b2  ;
printf,text=" kqt13.r6b2   =   %F ;",value= kqt13.r6b2  ;

assign,echo=terminal;


title,"Matched";

twiss,beta0=bir6b2;


plot, haxis=s, vaxis=betx,bety, colour=100,, vmax=1100;
plot, range=s.ds.l6.b2/e.ds.r6.b2, haxis=s, vaxis=betx,bety, colour=100;
plot, range=s.ds.l6.b2/e.ds.r6.b2, haxis=s, vaxis=betx,bety, colour=100, vmax=1e3;
plot, range=s.ds.l6.b2/e.ds.r6.b2, haxis=s, vaxis=mux,muy, colour=100;
plot, range=s.ds.l6.b2/e.ds.r6.b2, haxis=s, vaxis=dx, colour=100;



dmuxkickb2_tcsg=table(twiss,MKD.H5R6.B2,mux)-table(twiss,TCSP.A4L6.B2,mux);
dmuxkickb2=table(twiss,MKD.H5R6.B2,mux)-table(twiss,TCDQA.A4L6.B2,mux);
betxtcdqb2=table(twiss,TCDQA.A4L6.B2,betx);betytcdqb2=table(twiss,TCDQA.A4L6.B2,bety);
betxtcdsb2=table(twiss,TCDSA.4R6.B2,betx);betytcdsb2=table(twiss,TCDSA.4R6.B2,bety);
betxtcsgb2=table(twiss,TCSP.A4L6.B2,betx);betytcsgb2=table(twiss,TCSP.A4L6.B2,bety);
betxmkdb2 =table(twiss,MKD.H5R6.B2,betx);  betymkdb2=table(twiss,MKD.H5R6.B2,bety);
dxq5r6b2=abs(table(twiss,MQY.5R6.B2,dx));
dxq4l6b2=abs(table(twiss,MQY.4L6.B2,dx));
bxdumpb2=refbxdumpb2;
bydumpb2=refbydumpb2;
value,kq4.r6b2+0.004833837730000;
value,betxtcdqb2,betytcdqb2,betxtcdsb2,betytcdsb2,betxtcsgb2,betytcsgb2;
value,dmuxkickb2;
value,refbxdumpb2,refbydumpb2,refbdumpb2;
value,refdmuxkickb2_bds;

value,refmuxip6b2-eir6b2->mux;
value,refmuyip6b2-eir6b2->muy;

tarir6b2=tar;
value,kqt13.l6b2,kqt12.l6b2,kqtl11.l6b2,kq10.l6b2,kq9.l6b2,kq8.l6b2,kq5.l6b2,kq4.l6b2,
kq4.r6b2,kq5.r6b2,kq8.r6b2,kq9.r6b2,kq10.r6b2,kqtl11.r6b2,kqt12.r6b2,kqt13.r6b2;
value,tarir6b2;

betxip6b2=refbetxip6b2 ;
betyip6b2=refbetyip6b2 ;
alfxip6b2=refalfxip6b2 ;
alfyip6b2=refalfyip6b2 ;
dxip6b2 =refdxip6b2 ;
dpxip6b2=refdpxip6b2 ;


value,kq5.l6b2*scale;
value,kq5.r6b2*scale;


value,TAR ;



!muxip6b2 = 2.0000 ;betxip6b2= 1.96000000E+02 ;alfxip6b2 = 5.98285418E-01  ;
!muyip6b2 = 2.0300 ;betyip6b2= 1.79082800E+02;alfyip6b2 =-5.41937487E-01 ;
!dxip6b2 =  1.74610979E-01;dpxip6b2 = 1.56146811E-03 ;
!strategy for 11->22 : use all (kq4.r6b2 fixed),alfas,dx as injection,dpx=0,betas not constrained
!strategy for 22->28 : use all (kq4.r6b2 fixed),alfas, dx as injection,dpx=0,betas not constrained
!strategy for 22->82 : use all (kq4.r6b2 fixed),alfas, dx as injection,dpx=0,betas not constrained (around 320/180)



return;


